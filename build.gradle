group 'com.avioconsulting'
version '1.0-SNAPSHOT'

configurations {
    provided
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

sourceCompatibility = 1.5

repositories {
    mavenLocal()
    mavenCentral()
}

final String weblogicMavenPluginVersion = '12.2.1-2-0'

task pluginDescriptor(type: Exec) {
    commandLine 'mvn', '-e', '-B', 'org.apache.maven.plugins:maven-plugin-plugin:3.2:descriptor'
    def pomTextWithWeblogic = null
    doFirst {
        final File pom = project.file('pom.xml')
        install.repositories.mavenInstaller.pom.writeTo(pom)
        assert pom.file, "[$pom.canonicalPath] was not created"

        pom.text = pom.text.
                replace('<groupId>unknown</groupId>', "<groupId>${project.group}</groupId>").
                replace('<artifactId>empty-project</artifactId>', "<artifactId>${project.name}</artifactId>").
                replace('<version>0</version>', """
                                                              |<version>${version}</version>
                                                              |  <packaging>maven-plugin</packaging>
                                                              |  <build>
                                                              |    <directory>\${project.basedir}/build</directory>
                                                              |    <outputDirectory>\${project.build.directory}/classes/main</outputDirectory>
                                                              |  </build>
                                                              |""".stripMargin().trim())
        pomTextWithWeblogic = pom.text
        // This dependency causes problems and can't be scanned by the descriptor plugin
        pom.text = pom.text.replace("<dependency>\n      <groupId>com.oracle.weblogic</groupId>\n      <artifactId>weblogic-maven-plugin</artifactId>\n      <version>${weblogicMavenPluginVersion}</version>\n      <scope>compile</scope>\n    </dependency>", '')
    }
    doLast {
        def pom = project.file('pom.xml')
        pom.text = pomTextWithWeblogic
        final pluginDescriptor = new File((File) project.compileGroovy.destinationDir, 'META-INF/maven/plugin.xml')
        assert pluginDescriptor.file, "[$pluginDescriptor.canonicalPath] was not created"
        println "Plugin descriptor file:$pluginDescriptor.canonicalPath is created successfully"
    }
}

project.compileGroovy.doLast { pluginDescriptor.execute() }

dependencies {
    compile 'commons-io:commons-io:1.3.2'
    compile 'org.apache.ant:ant:1.9.7'
    compile 'org.apache.maven:maven-plugin-api:3.3.9'
    compile group: 'com.oracle.weblogic', name: 'weblogic-maven-plugin', version: weblogicMavenPluginVersion
    //runtime 'com.oracle.fmwshare:fmwshare-wlst-dependencies:12.2.1-0-0@pom'
    //runtime 'com.oracle.ess:ess-wlst-dependencies:12.2.1-0-0@pom'
    provided 'org.apache.maven.plugin-tools:maven-plugin-annotations:3.5'
    def mvnProject = 'org.apache.maven:maven-project:3.0-alpha-2'
    // use explicitly in tests but Maven will provide the dependency at runtime
    provided mvnProject
    testCompile mvnProject
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.apache.commons:commons-io:1.3.2'
}
