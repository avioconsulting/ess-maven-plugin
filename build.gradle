group 'com.avioconsulting'
version '1.0-SNAPSHOT'

configurations {
    provided
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

sourceCompatibility = 1.5

repositories {
    mavenLocal()
    mavenCentral()
}

def moveDependencyToEnd(Node dependency, Node dependencies) {
    def individualDeps = dependencies.children()
    individualDeps.remove(dependency)
    individualDeps.add(dependency)
}

def removeExclusions(Node dependencies) {
    dependencies.children().each { dep ->
        def exclusions = dep.exclusions
        if (exclusions) {
            dep.remove(exclusions)
        }
    }
}

task pluginDescriptor(type: Exec) {
    commandLine 'mvn', '-e', '-B', 'org.apache.maven.plugins:maven-plugin-plugin:3.2:descriptor'
    def findDependency = { pomNode ->
        pomNode.dependencies.dependency.find { d -> d.artifactId[0].value()[0] == 'weblogic-maven-plugin' }
    }

    def existingScope = null
    doFirst {
        def pom = project.file('pom.xml')
        install.repositories.mavenInstaller.pom.writeTo(pom)
        assert pom.file, "[$pom.canonicalPath] was not created"
        def pomNode = new XmlParser().parse(pom)
        pomNode.groupId[0].value = project.group
        pomNode.artifactId[0].value = project.name
        pomNode.version[0].value = version
        pomNode.appendNode('packaging', 'maven-plugin')
        def buildNode = pomNode.appendNode('build')
        buildNode.appendNode('directory', '\${project.basedir}/build')
        buildNode.appendNode('outputDirectory', '\${project.build.directory}/classes/main')

        // descriptor plugin doesn't like scanning this, so exclude it from scope temporarily
        def wlDependency = findDependency(pomNode)
        existingScope = wlDependency.scope[0].value()[0]
        wlDependency.scope[0].value = 'test'
        groovy.xml.XmlUtil.serialize(pomNode, new FileWriter(pom))
    }
    doLast {
        def pom = project.file('pom.xml')
        Node pomNode = new XmlParser().parse(pom)
        Node wlDependency = findDependency(pomNode)
        // put the scope back
        wlDependency.scope[0].value = existingScope
        def dependencyList = pomNode.dependencies[0]
        // the weblogic-maven plugin has to be at the end of the dependency list in the POM
        moveDependencyToEnd wlDependency, dependencyList
        // for some reason, the Gradle Maven plugin excludes transitive dependencies on 2 key items
        // when it generates pom.xml, fix that
        removeExclusions dependencyList
        groovy.xml.XmlUtil.serialize(pomNode, new FileWriter(pom))
        final pluginDescriptor = new File((File) project.compileGroovy.destinationDir, 'META-INF/maven/plugin.xml')
        assert pluginDescriptor.file, "[$pluginDescriptor.canonicalPath] was not created"
        println "Plugin descriptor file:$pluginDescriptor.canonicalPath is created successfully"
    }
}

project.compileGroovy.doLast { pluginDescriptor.execute() }

dependencies {
    compile 'commons-io:commons-io:1.3.2'
    compile 'org.apache.ant:ant:1.9.7'
    compile 'org.apache.maven:maven-plugin-api:3.3.9'
    runtime 'com.oracle.fmwshare:fmwshare-wlst-dependencies:12.2.1-0-0@pom'
    runtime 'com.oracle.ess:ess-wlst-dependencies:12.2.1-0-0@pom'
    compile group: 'com.oracle.weblogic', name: 'weblogic-maven-plugin', version: '12.2.1-2-0'
    provided 'org.apache.maven.plugin-tools:maven-plugin-annotations:3.5'
    def mvnProject = 'org.apache.maven:maven-project:3.0-alpha-2'
    // use explicitly in tests but Maven will provide the dependency at runtime
    provided mvnProject
    testCompile mvnProject
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.apache.commons:commons-io:1.3.2'
}
